
pluginManagement {
    repositories {
        mavenLocal()
        gradlePluginPortal()
    }

    final File versionFile = new File(rootDir.parentFile, "plugins/version.txt")
    final String pluginsVersion = versionFile.readLines().first()
    plugins {
        id("de.fayard.refreshVersions").version(pluginsVersion)
    }
}

plugins {
    id("com.gradle.enterprise").version("3.8")
    id 'de.fayard.refreshVersions'
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        google()
    }
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}

rootProject.name = 'sample-groovy'

refreshVersions {
    featureFlags {
        enable 'LIBS'
        disable 'GRADLE_UPDATES'
    }

    // ignore dependencies among a blacklist of version keys
    rejectVersionIf {
        def blacklist =  ["version.retrofit", "version.okhttp3"]
        versionKey in blacklist
    }

    // ignore dependencies among a blacklist of maven groups
    rejectVersionIf {
        def blacklist = ["com.squareup.retrofit", "com.squareup.okhttp3"]
        moduleId.group in blacklist
    }

    // ignore all non-stable releases
    rejectVersionIf {
        candidate.stabilityLevel != StabilityLevel.Stable
    }

    // Or maybe you want to see alpha versions if you are already using an alpha version, otherwise you want to see only stable versions
    rejectVersionIf {
        candidate.stabilityLevel.isLessStableThan(current.stabilityLevel)
    }

}
